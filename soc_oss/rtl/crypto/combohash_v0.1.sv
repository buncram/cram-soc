`include "template.sv"

import hash_pkg::*;
import scedma_pkg::*;

module combohash #(
        parameter SEGCNT = 7,
        parameter ERRCNT = 8,
        parameter INTCNT = 8
    )(

    input  logic clk, resetn, cmsatpg, cmsbist,

    input   bit             start,
    output  bit             busy,
    output  bit             done,
    input hashfunc_e        cr_func,
    input hashfuncopt_t     cr_opt,
    input scedma_pkg::adr_t [0:SEGCNT-1]   cr_segptrstart,

    output  chnlreq_t       chnl_rpreq, chnl_wpreq   ,
    input   chnlres_t       chnl_rpres, chnl_wpres   ,

    output logic [0:ERRCNT-1]      err,
    output logic [0:INTCNT-1]      intr
);

    localparam [7:0] SEGID_LKEY = scedma_pkg::SEGID_LKEY ;
    localparam [7:0] SEGID_KEY  = scedma_pkg::SEGID_KEY  ;
    localparam [7:0] SEGID_SKEY = scedma_pkg::SEGID_SKEY ;
    localparam [7:0] SEGID_SCRT = scedma_pkg::SEGID_SCRT ;
    localparam [7:0] SEGID_MSG  = scedma_pkg::SEGID_MSG  ;
    localparam [7:0] SEGID_HOUT = scedma_pkg::SEGID_HOUT ;
    localparam [7:0] SEGID_SOB  = scedma_pkg::SEGID_SOB  ;

    localparam segcfg_t SEG_LKEY = scedma_pkg::SEGCFGS[SEGID_LKEY];
    localparam segcfg_t SEG_KEY  = scedma_pkg::SEGCFGS[SEGID_KEY ];
    localparam segcfg_t SEG_SKEY = scedma_pkg::SEGCFGS[SEGID_SKEY];
    localparam segcfg_t SEG_SCRT = scedma_pkg::SEGCFGS[SEGID_SCRT];
    localparam segcfg_t SEG_MSG  = scedma_pkg::SEGCFGS[SEGID_MSG ];
    localparam segcfg_t SEG_HOUT = scedma_pkg::SEGCFGS[SEGID_HOUT];
    localparam segcfg_t SEG_SOB  = scedma_pkg::SEGCFGS[SEGID_SOB ];

// typedef
// ■■■■■■■■■■■■■■■

    typedef enum bit[7:0] {
        MFSM_IDLE      = 'h00,
        MFSM_DONE      = 'hff,
        MFSM_HF        = 'h20,
        MFSM_LD_ST     = 'h10,
        MFSM_LD_LKEY   = 'h11,
        MFSM_LD_MSG0   = 'h12,
        MFSM_LD_MSG    = 'h13,
        MFSM_LD_PASS1  = 'h14,
        MFSM_LD_KEY    = 'h15,
        MFSM_LD_PAD    = 'h16,
        MFSM_LD_SECRET = 'h17,
        MFSM_WB_HOUT   = 'h30,
        MFSM_WB_SOB    = 'h31,
        MFSM_WB_KEY    = 'h32
    } mfsm_e;

    localparam adr_t RAMSEG_ST = hash_pkg::RAMSEG_ST;
    localparam adr_t RAMSEG_MSG = hash_pkg::RAMSEG_MSG;

    localparam scedma_pkg::segcfg_t HASHSEG_ST = '{
            segid        : '0,
            segtype      : ST_NONE,
            ramsel       : '0,
            segaddr      :  RAMSEG_ST,
            segsize      : 'd32,
            isfifo       : '0,
            isfifostream : '0,
            fifoid       : '0
        };

    localparam scedma_pkg::segcfg_t HASHSEG_MSG = '{
            segid        : '1,
            segtype      : ST_NONE,
            ramsel       : '0,
            segaddr      : RAMSEG_MSG,
            segsize      : 'd16,
            isfifo       : '0,
            isfifostream : '0,
            fifoid       : '0
        };

    localparam scedma_pkg::segcfg_t SEG_PAD = '{ 
            segid        : '1,
            segtype      : ST_NONE,
            ramsel       : '0,
            segaddr      : '0,
            segsize      : 'd64,
            isfifo       : '0,
            isfifostream : '0,
            fifoid       : '0
        };

    bit [15:0]  opt_cnt;
    bit         opt_ifstart;
    bit         opt_ifsob;
    bit         opt_check;
    mfsm_e mfsm, mfsmnext;
    bit [15:0] hashcnt, hashcntnext;
    bit mfsmtog, mfsmdone;
    hashtype_e cfg_hashtype;
    logic hash_start, chnli_start, chnlo_start;
    logic hash_busy, chnli_busy, chnlo_busy;
    logic hash_done, chnli_done, chnlo_done;
    logic [1:0] ramerror;
    scedma_pkg::adr_t MSGSIZE, STSIZE;
    logic hcore_en, hcore_ramrd, chnli_en, chnlo_en;
    chnlreq_t hcore_req, chnlo_rpreq, chnli_wpreq, hramreq;
    chnlres_t hramres;
    chnlcfg_t chnli_cfg, chnlo_cfg;
    scedma_pkg::adr_t hcore_ramaddr;
    scedma_pkg::dat_t hcore_ramrdat, hcore_ramwdat;
    logic hcore_ramwr;
    bit [7:0] chnlo_intr, chnli_intr;
    bit optlock;

// define cmd/opt
// ■■■■■■■■■■■■■■■

    assign optlock = ( start & ( mfsm == MFSM_IDLE));
    `theregrn( opt_ifstart ) <= optlock ? cr_opt.ifstart : opt_ifstart ;
    `theregrn( opt_cnt     ) <= optlock ? cr_opt.hashcnt : opt_cnt     ;
    `theregrn( opt_ifsob   ) <= optlock ? cr_opt.ifsob   : opt_ifsob   ;
    `theregrn( opt_check   ) <= optlock ? cr_opt.scrtchk : opt_check   ;

// mfsm
// ■■■■■■■■■■■■■■■
    `theregfull(clk, resetn, mfsm, MFSM_IDLE ) <= ( start & ( mfsm == MFSM_IDLE)) | mfsmdone ? mfsmnext : mfsm;
    `theregrn( hashcnt ) <= hash_done ? hashcntnext : hashcnt;
    `theregrn( mfsmtog ) <= ( mfsm != mfsmnext ) & ( mfsmnext != MFSM_IDLE );

    always_comb begin
        mfsmnext = mfsm;
        mfsmdone = '0;
        hashcntnext = hashcnt;
        case (cr_func)
            HF_SHA256, 
            HF_SHA512, 
            HF_RIPMD, 
            HF_BLK2s, 
            HF_BLK2b, 
            HF_BLK3:
                case( mfsm )
                    MFSM_IDLE:
                        if( opt_ifstart )
                                                mfsmnext = MFSM_LD_MSG;
                        else                    
                                                mfsmnext = MFSM_LD_ST;
                    MFSM_LD_ST : begin
                                                mfsmnext = MFSM_LD_MSG;    mfsmdone = chnli_done;
                        end
                    MFSM_LD_MSG: begin     
                                                mfsmnext = MFSM_HF;        mfsmdone = chnli_done;
                        end
                    MFSM_HF :
                        if( opt_cnt == hashcnt )begin
                                                mfsmnext = MFSM_WB_HOUT;   mfsmdone = hash_done;
                                                                                hashcntnext = '0;
                        end
                        else begin
                                                mfsmnext = MFSM_LD_MSG;    mfsmdone = hash_done;
                                                                                hashcntnext = hashcnt + 'h1;
                        end
                    MFSM_WB_HOUT :
                        if( opt_ifsob )begin
                                                mfsmnext = MFSM_WB_SOB;    mfsmdone = chnlo_done;

                        end
                        else begin
                                                mfsmnext = MFSM_DONE;           mfsmdone = chnlo_done;
                        end
                    MFSM_WB_SOB : begin
                                                mfsmnext = MFSM_DONE;           mfsmdone = chnlo_done;
                        end
                    MFSM_DONE: begin
                                                mfsmnext = MFSM_IDLE;           mfsmdone = 'h1;
                        end
                endcase
            HF_HMAC256_KEYHASH, 
            HF_HMAC512_KEYHASH:
                case( mfsm )
                    MFSM_IDLE:
                        if( opt_ifstart )
                                                mfsmnext = MFSM_LD_LKEY;
                        else                    
                                                mfsmnext = MFSM_LD_ST;
                    MFSM_LD_ST : begin
                                                mfsmnext = MFSM_LD_LKEY;        mfsmdone = chnli_done;
                        end
                    MFSM_LD_LKEY: begin     
                                                mfsmnext = MFSM_HF;             mfsmdone = chnli_done;
                        end
                    MFSM_HF :
                        if( opt_cnt == hashcnt )begin
                                                mfsmnext = MFSM_WB_KEY;         mfsmdone = hash_done;
                                                                                hashcntnext = '0;
                        end
                        else begin
                                                mfsmnext = MFSM_LD_LKEY;        mfsmdone = hash_done;
                                                                                hashcntnext = hashcnt + 'h1;
                        end
                    MFSM_WB_KEY : begin
                                                mfsmnext = MFSM_DONE;           mfsmdone = chnlo_done;
                        end
                    MFSM_DONE: begin
                                                mfsmnext = MFSM_IDLE;           mfsmdone = 'h1;
                        end
                endcase
            HF_HMAC256_PASS1, 
            HF_HMAC512_PASS1:
                case( mfsm )
                    MFSM_IDLE:
                        if( opt_ifstart )
                                                mfsmnext = MFSM_LD_KEY ;
                        else                    
                                                mfsmnext = MFSM_LD_ST;
                    MFSM_LD_ST : begin
                                                mfsmnext = MFSM_LD_MSG;         mfsmdone = chnli_done;
                        end 
                    MFSM_LD_KEY : begin      
                                                mfsmnext = MFSM_LD_MSG0;        mfsmdone = chnli_done;
                        end 
                    MFSM_LD_MSG0: begin      
                                                mfsmnext = MFSM_HF;             mfsmdone = chnli_done;
                        end 
                    MFSM_LD_MSG: begin       
                                                mfsmnext = MFSM_HF;             mfsmdone = chnli_done;
                        end 
                    MFSM_HF :    
                        if( opt_cnt == hashcnt )begin   
                                                mfsmnext = MFSM_WB_HOUT;        mfsmdone = hash_done;
                                                                                hashcntnext = '0;
                        end 
                        else begin  
                                                mfsmnext = MFSM_LD_MSG;         mfsmdone = hash_done;
                                                                                hashcntnext = hashcnt + 'h1;
                        end 
                    MFSM_WB_HOUT:    
                        begin   
                                                mfsmnext = MFSM_DONE;           mfsmdone = chnlo_done;
                        end 
                    MFSM_DONE: begin 
                                                mfsmnext = MFSM_IDLE;           mfsmdone = 'h1;
                        end
                endcase
            HF_HMAC256_PASS2, 
            HF_HMAC512_PASS2:
                case( mfsm )
                    MFSM_IDLE:
                                                mfsmnext = MFSM_LD_KEY ;
                    MFSM_LD_KEY  : begin
                                                mfsmnext = MFSM_LD_PASS1;       mfsmdone = chnli_done;
                        end 
                    MFSM_LD_PASS1: begin 
                                                mfsmnext = MFSM_HF;             mfsmdone = chnli_done;
                        end 
                    MFSM_HF :    
                        if( hashcnt == 0 )begin 
                                                mfsmnext = MFSM_LD_PAD;         mfsmdone = hash_done;
                                                                                hashcntnext = hashcnt + 'h1;
                        end 
                        else if( opt_check )begin   
                                                mfsmnext = MFSM_LD_SECRET;      mfsmdone = hash_done;
                                                                                hashcntnext = '0;
                        end else begin  
                                                mfsmnext = MFSM_WB_HOUT;        mfsmdone = hash_done;
                                                                                hashcntnext = '0;
                        end 
                    MFSM_LD_PAD: begin   
                                                mfsmnext = MFSM_HF;             mfsmdone = chnli_done;
                        end 
                    MFSM_LD_SECRET: begin    
                                                mfsmnext = MFSM_WB_HOUT;        mfsmdone = chnli_done;
                        end 
                    MFSM_WB_HOUT:    
                        if( opt_ifsob )begin    
                                                mfsmnext = MFSM_WB_SOB;         mfsmdone = chnlo_done;
                        end 
                        else begin  
                                                mfsmnext = MFSM_DONE;           mfsmdone = chnlo_done;
                        end 
                    MFSM_WB_SOB : begin  
                                                mfsmnext = MFSM_DONE;           mfsmdone = chnlo_done;
                        end 
                    MFSM_DONE: begin 
                                                mfsmnext = MFSM_IDLE;           mfsmdone = 'h1;
                        end
                endcase
            default : /* default */
                begin
                    mfsmnext = mfsm;
                    mfsmdone = '0;
                    hashcntnext = hashcnt;
                end
        endcase
    end

// subcore, chnl
// ■■■■■■■■■■■■■■■

    `theregrn( hash_start ) <= mfsmtog & ( mfsm == MFSM_HF  );

    always_comb
    case (cr_func)
            HF_SHA256:
                    cfg_hashtype = HT_SHA256;
            HF_SHA512:
                    cfg_hashtype = HT_SHA512;
            HF_RIPMD:
                    cfg_hashtype = HT_RIPMD;
            HF_BLK2s:
                    cfg_hashtype = HT_BLK2s;
            HF_BLK2b:
                    cfg_hashtype = HT_BLK2b;
            HF_BLK3:
                    cfg_hashtype = HT_BLK3;
            HF_SHA3:
                    cfg_hashtype = HT_SHA3;
            HF_HMAC256_KEYHASH,
            HF_HMAC256_PASS1,
            HF_HMAC256_PASS2:
                    cfg_hashtype = HT_SHA256;
            HF_HMAC512_KEYHASH,
            HF_HMAC512_PASS1,
            HF_HMAC512_PASS2:
                    cfg_hashtype = HT_SHA512;
         default : 
                    cfg_hashtype = HT_NONE;
     endcase 

    always_comb
    case(cfg_hashtype)
        HT_SHA256: begin  MSGSIZE = 16; STSIZE =  8;  end
        HT_SHA512: begin  MSGSIZE = 32; STSIZE = 16;  end
        HT_RIPMD:  begin  MSGSIZE = 16; STSIZE =  5;  end
        HT_BLK2s:  begin  MSGSIZE = 32; STSIZE = 16;  end
        HT_BLK2b:  begin  MSGSIZE = 16; STSIZE =  8;  end
        HT_BLK3:   begin  MSGSIZE = 32; STSIZE = 16;  end
        HT_SHA3:   begin  MSGSIZE = 16; STSIZE =  8;  end
        default :  begin  MSGSIZE =  0; STSIZE =  0;  end
    endcase


    assign hcore_en = ( mfsm == MFSM_HF );
    assign hcore_ramrd = 1'b1;

    hashcore hcore
    (
        .clk    (clk),
        .resetn (resetn),
        .start  (hash_start),
        .busy   (hash_busy),
        .done   (hash_done),
        .cfg_hashtype (cfg_hashtype),
        .cfg_firsthash(opt_ifstart),
        .cfg_finalhash('0),

        .ramaddr(hcore_ramaddr[9:0]),
        .ramrdat32(hcore_ramrdat),
        .ramwr32(hcore_ramwr),
        .ramwdat32(hcore_ramwdat)
    );

    assign hcore_req.segaddr = hcore_ramaddr|'0;
    assign hcore_req.segptr  = '0;
    assign hcore_req.segrd   = ~hcore_ramwr;
    assign hcore_req.segwr   = hcore_ramwr;
    assign hcore_req.segwdat = hcore_ramwdat;
    assign hcore_ramrdat     = hramres.segrdat;

    always_comb begin
        case(mfsm)
            MFSM_LD_ST     :
                begin
                    chnli_cfg.rpsegcfg = SEG_HOUT;
                    chnli_cfg.wpsegcfg = HASHSEG_ST;
                    chnli_cfg.rpptr_start = cr_segptrstart[SEGID_HOUT];
                    chnli_cfg.transsize = STSIZE;
                    chnli_en = '1;
                end
            MFSM_LD_LKEY   :
                begin
                    chnli_cfg.rpsegcfg = SEG_LKEY;
                    chnli_cfg.wpsegcfg = HASHSEG_MSG;
                    chnli_cfg.rpptr_start = cr_segptrstart[SEGID_LKEY];
                    chnli_cfg.transsize = MSGSIZE;
                    chnli_en = '1;
                end
            MFSM_LD_MSG0   :
                begin
                    chnli_cfg.rpsegcfg = SEG_MSG;
                    chnli_cfg.wpsegcfg = HASHSEG_MSG;
                    chnli_cfg.rpptr_start = cr_segptrstart[SEGID_MSG];
                    chnli_cfg.transsize = MSGSIZE;
                    chnli_en = '1;
                end
            MFSM_LD_MSG    :
                begin
                    chnli_cfg.rpsegcfg = SEG_MSG;
                    chnli_cfg.wpsegcfg = HASHSEG_MSG;
                    chnli_cfg.rpptr_start = cr_segptrstart[SEGID_MSG];
                    chnli_cfg.transsize = MSGSIZE;
                    chnli_en = '1;
                end
            MFSM_LD_PASS1  :
                begin
                    chnli_cfg.rpsegcfg = SEG_HOUT;
                    chnli_cfg.wpsegcfg = HASHSEG_MSG;
                    chnli_cfg.rpptr_start = cr_segptrstart[SEGID_HOUT];
                    chnli_cfg.transsize = STSIZE;
                    chnli_en = '1;
                end
            MFSM_LD_KEY    :
                begin
                    chnli_cfg.rpsegcfg = SEG_KEY; //SEG_SKEY##
                    chnli_cfg.wpsegcfg = HASHSEG_MSG;
                    chnli_cfg.rpptr_start = cr_segptrstart[SEGID_KEY];
                    chnli_cfg.transsize = STSIZE;
                    chnli_en = '1;
                end
            MFSM_LD_PAD    :
                begin
                    chnli_cfg.rpsegcfg = SEG_PAD;
                    chnli_cfg.wpsegcfg = HASHSEG_MSG;
//                    chnli_cfg.rpptr_start = crptr_msg;
                    chnli_cfg.transsize = MSGSIZE;
                    chnli_en = '1;
                end
            MFSM_LD_SECRET :
                begin
                    chnli_cfg.rpsegcfg = SEG_SCRT;
                    chnli_cfg.wpsegcfg = HASHSEG_ST;
                    chnli_cfg.rpptr_start = cr_segptrstart[SEGID_SCRT];
                    chnli_cfg.transsize = STSIZE;
                    chnli_en = '1;
                end
            default : /* default */
                begin
                    chnli_cfg.rpsegcfg = SEG_MSG;
                    chnli_cfg.wpsegcfg = HASHSEG_MSG;
                    chnli_cfg.rpptr_start = cr_segptrstart[SEGID_MSG];
                    chnli_cfg.transsize = MSGSIZE;
                    chnli_en = '0;
                end
        endcase
    end

    assign chnli_cfg.chnlid = '0;
    assign chnli_cfg.wpptr_start = '0;
    assign chnli_cfg.opt_ltx = '0;
    assign chnli_cfg.opt_xor = '0;
    assign chnli_cfg.opt_cmpp = '0;
    assign chnli_cfg.opt_prm = '0;
    assign chnli_cfg.wpffen = '0;

    `theregrn( chnli_start ) <= mfsmtog &
                                   (( mfsm == MFSM_IDLE      )|
                                    ( mfsm == MFSM_DONE      )|
                                    ( mfsm == MFSM_HF        )|
                                    ( mfsm == MFSM_LD_ST     )|
                                    ( mfsm == MFSM_LD_LKEY   )|
                                    ( mfsm == MFSM_LD_MSG0   )|
                                    ( mfsm == MFSM_LD_MSG    )|
                                    ( mfsm == MFSM_LD_PASS1  )|
                                    ( mfsm == MFSM_LD_KEY    )|
                                    ( mfsm == MFSM_LD_PAD    )|
                                    ( mfsm == MFSM_LD_SECRET ));

    `theregrn( chnlo_start ) <= mfsmtog &
                                   (( mfsm == MFSM_WB_HOUT  )|
                                    ( mfsm == MFSM_WB_SOB   )|
                                    ( mfsm == MFSM_WB_KEY   ));

    always_comb begin
        case(mfsm)
            MFSM_WB_HOUT     :
                begin
                    chnlo_cfg.rpsegcfg = HASHSEG_ST;
                    chnlo_cfg.wpsegcfg = SEG_HOUT;
                    chnlo_cfg.wpptr_start = cr_segptrstart[SEGID_HOUT];
                    chnlo_cfg.transsize = STSIZE;
                end
            MFSM_WB_SOB   :
                begin
                    chnlo_cfg.rpsegcfg = HASHSEG_ST;
                    chnlo_cfg.wpsegcfg = SEG_SOB;
                    chnlo_cfg.wpptr_start = cr_segptrstart[SEGID_SOB];
                    chnlo_cfg.transsize = STSIZE;
                end
            MFSM_WB_KEY   :
                begin
                    chnlo_cfg.rpsegcfg = HASHSEG_ST;
                    chnlo_cfg.wpsegcfg = SEG_KEY;// ##SEG_SKEY
                    chnlo_cfg.wpptr_start = cr_segptrstart[SEGID_KEY];
                    chnlo_cfg.transsize = STSIZE;
                end
            default : /* default */
                begin
                    chnlo_cfg.rpsegcfg = HASHSEG_ST;
                    chnlo_cfg.wpsegcfg = SEG_HOUT;
                    chnlo_cfg.wpptr_start = cr_segptrstart[SEGID_HOUT];
                    chnlo_cfg.transsize = STSIZE;
                end
        endcase
    end

    assign chnlo_cfg.chnlid = '0;
    assign chnlo_cfg.rpptr_start = '0;
    assign chnlo_cfg.opt_ltx = '0;
    assign chnlo_cfg.opt_xor = '0;
    assign chnlo_cfg.opt_cmpp = '0;
    assign chnlo_cfg.opt_prm = '0;
    assign chnlo_cfg.wpffen = '0;

    scedma_chnl chnli(
        .clk,
        .resetn,
        .thecfg   (chnli_cfg   ),
        .start    (chnli_start ),
        .busy     (chnli_busy  ),
        .done     (chnli_done  ),
        .rpreq    (chnl_rpreq  ),
        .rpres    (chnl_rpres  ),
        .wpreq    (chnli_wpreq ),
        .wpres    (hramres     ),
        .intr     (chnli_intr  )
    );

    scedma_chnl chnlo(
        .clk,
        .resetn,
        .thecfg   (chnlo_cfg   ),
        .start    (chnlo_start ),
        .busy     (chnlo_busy  ),
        .done     (chnlo_done  ),
        .rpreq    (chnlo_rpreq ),
        .rpres    (hramres     ),
        .wpreq    (chnl_wpreq  ),
        .wpres    (chnl_wpres  ),
        .intr     (chnlo_intr  )
    );

    assign hramreq  = hcore_en ? hcore_req : chnli_en ? chnli_wpreq : chnlo_rpreq ;

// subcore, chnl
// ■■■■■■■■■■■■■■■

    // 768 x 32, 3KB
    // the real ram is 768x36

    localparam sramcfg_t thecfg = {
        AW: 10,
        DW: 32,
        KW: 32,
        PW: 4,
        WCNT: 1024*3,
        AWX: 5,
        isBWEN: '1,
        isSCMB: '1,
        isPRT:  '1,
        EVITVL:  15
    };


    logic [9:0] cryptoramaddr;
    assign cryptoramaddr = hramreq.segaddr + hramreq.segptr ;

    cryptoram #(
        .ramname    ("HRAM"), // HRAM, PRAM, ARAM, SCERAM
        .thecfg     (thecfg)
    )m(
        .clk, .resetn, .cmsatpg, .cmsbist,
        .ramaddr (cryptoramaddr ),
        .ramen('1),
        .ramrd(hramreq.segrd),
        .ramwr({4{hramreq.segwr}}),
        .ramwdat(hramreq.segwdat),
        .ramrdat(hramres.segrdat),
        .ramready(hramres.segready),
        .ramerror(ramerror)
    );
    assign hramres.segrdatvld = 1'b1;

// err/intr
// ■■■■■■■■■■■■■■■


    `theregrn( intr[0] ) <= ( mfsm == MFSM_DONE );
    `theregrn( err[0:1] ) <= ramerror;

endmodule
